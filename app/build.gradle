apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.crashlytics'

android {
    configurations.configureEach {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.2'
    }
    compileSdk 34
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }
    testOptions.unitTests {
        includeAndroidResources = true
    }
    defaultConfig {
        applicationId "be.florien.anyflow"
        multiDexEnabled true
        minSdkVersion 23
        targetSdkVersion 33
        versionCode 10
        versionName "0.4.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildFeatures {
        dataBinding = true
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            minifyEnabled false
            shrinkResources false
        }
        mock {
            initWith(debug)
            applicationIdSuffix '.mock'
            minifyEnabled false
            shrinkResources false
            debuggable true
        }
        release {
            minifyEnabled false
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        mock.java.srcDirs = ['src/main/java', 'src/mock/java']
        androidTest.java.srcDirs += "src/sharedTest/java"
        test.java.srcDirs += "src/sharedTest/java"
    }
    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'
    namespace 'be.florien.anyflow'
}

dependencies {
    //Modules //todo check dependencies still needed and the one we could remove
    implementation project(':architecture-di')
    implementation project(':common-management')
    implementation project(":common-ui")
    implementation project(':common-ui-data')
    implementation project(":data-local")
    implementation project(":data-server")
    implementation project(":feature-auth-domain")
    implementation project(":feature-auth-ui")
    implementation project(":feature-library-domain")
    implementation project(":feature-library-ui")
    implementation project(':logging')
    implementation project(':utils')
    implementation project(':management-tags')
    implementation project(':management-filters')
    implementation project(':management-playlist')
    implementation project(':management-podcast')
    implementation project(':resources')
    //base
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "androidx.core:core-ktx:1.12.0"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-service:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'com.simplecityapps:recyclerview-fastscroll:2.0.1'
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    //DI
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    implementation 'org.glassfish:javax.annotation:10.0-b28'
    //internet
    implementation 'com.squareup.retrofit2:converter-jackson:2.9.0'

    //glide
    implementation 'com.github.bumptech.glide:glide:4.14.1'
    kapt 'com.github.bumptech.glide:compiler:4.14.1'
    implementation "com.github.bumptech.glide:okhttp3-integration:4.14.1"
    //ExoPlayer
    implementation 'androidx.media3:media3-session:1.2.1'
    implementation "androidx.media3:media3-exoplayer:1.3.0"
    implementation "androidx.media3:media3-ui:1.3.0"
    implementation "androidx.media3:media3-datasource-okhttp:1.3.0"
    implementation "com.google.android.exoplayer:extension-mediasession:2.19.1"
    //Room
    //todo add another layer of abstraction on top of libraryDatabase in data-local and remove these dependencies
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    //Paging
    implementation 'androidx.paging:paging-runtime-ktx:3.2.1'
    //Tests
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.11.0'
    testImplementation "android.arch.core:core-testing:1.1.1"
    testImplementation 'com.google.truth:truth:1.0.1'
    androidTestImplementation 'com.squareup.okhttp3:mockwebserver:4.11.0'
    androidTestImplementation "androidx.arch.core:core-testing:2.2.0"
    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.2.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'com.google.truth:truth:1.0.1'
    androidTestImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.3'
}
repositories {
    mavenCentral()
    google()
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'kotlin-parcelize'